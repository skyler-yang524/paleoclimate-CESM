#! /bin/csh -f

set mach = niagara

#################################################################################
if ($PHASE == set_batch) then
#################################################################################

source ./Tools/ccsm_getenv || exit -1

set ntasks  = `${CASEROOT}/Tools/taskmaker.pl -sumonly`
set maxthrds = `${CASEROOT}/Tools/taskmaker.pl -maxthrds`
@ nodes = $ntasks / ${MAX_TASKS_PER_NODE}
if ( $ntasks % ${MAX_TASKS_PER_NODE} > 0) then
  @ nodes = $nodes + 1
  @ ntasks = $nodes * ${MAX_TASKS_PER_NODE}
endif
@ taskpernode = ${MAX_TASKS_PER_NODE} / ${maxthrds}
set qname = batch
set tlimit = "23:59:00"

#--- Job name is first fifteen characters of case name ---
#set jobname = `echo ${CASE} | cut -c1-15`
# cgf mod: job name IS case name!
set jobname = `echo ${CASE}`

if ($?TESTMODE) then
 set file = $CASEROOT/${CASE}.test 
else
 set file = $CASEROOT/${CASE}.run 
endif

cat >! $file << EOF1
#!/bin/csh -f
#===============================================================================
# USERDEFINED(niagara)
# This is where the batch submission is set.  The above code computes
# the total number of tasks, nodes, and other things that can be useful
# here.  Use PBS, BSUB, or whatever the local environment supports.
#===============================================================================

#SBATCH --account=rrg-pheron
#SBATCH --job-name=${CASE}
#SBATCH --time=${tlimit}
#SBATCH --nodes=${nodes}
#SBATCH --ntasks-per-node=40
#SBATCH --partition=debug
#SBATCH --mail-user=skyler.yang@mail.utoronto.ca
#SBATCH --mail-type=FAIL


source $MODULESHOME/init/csh
module load NiaEnv/2018a
module load perl/5.26.1
module load cmake/3.10.3
module load intel/2018.2
module load intelmpi/2018.2
#module load hdf5/1.8.20
#module load netcdf/4.6.1
module load hdf5-mpi/1.8.20
module load netcdf-mpi/4.6.1
#module load pnetcdf/1.9.0
limit coredumpsize 1000000
unlimit stacksize


EOF1

#################################################################################
else if ($PHASE == set_exe) then
#################################################################################

set maxthrds = `${CASEROOT}/Tools/taskmaker.pl -maxthrds`
set maxtasks = `${CASEROOT}/Tools/taskmaker.pl -sumtasks`

cat >> ${CASEROOT}/${CASE}.run << EOF1

# -------------------------------------------------------------------------
# # Run the model
# -------------------------------------------------------------------------

sleep 25
cd \$RUNDIR
echo "\`date\` -- CSM EXECUTION BEGINS HERE" 

setenv OMP_NUM_THREADS ${maxthrds}

#===============================================================================
# USERDEFINED
# edit job launching
#===============================================================================

#mpiexec -n ${maxtasks} \$EXEROOT/cesm.exe >&! cesm.log.\$LID
mpirun -np ${maxtasks} \$EXEROOT/cesm.exe >&! cesm.log.\$LID

wait
echo "\`date\` -- CSM EXECUTION HAS FINISHED" 

EOF1


#################################################################################
else if ($PHASE == set_larch) then
#################################################################################

   #This is a place holder for a long-term archiving script


#################################################################################
else
#################################################################################

    echo "  PHASE setting of $PHASE is not an accepted value"
    echo "  accepted values are set_batch, set_exe and set_larch"
    exit 1

#################################################################################
endif
#################################################################################
